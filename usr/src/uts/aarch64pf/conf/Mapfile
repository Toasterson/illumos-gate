OUTPUT_FORMAT("elf64-littleaarch64")
OUTPUT_ARCH(aarch64)
ENTRY(_start)

SECTIONS
{
	. = 0xfffffffffe000000;
	.text : {
		*(.plt*)
		*(.text)
		*(.text.*)
		. = ALIGN(0x10);
		*(.rodata)
		*(.rodata.*)
		. = ALIGN(0x10);
	}
	. = ALIGN(0x1000);
	.data : {
		*(.data)
		*(.data.*)
		. = ALIGN(0x8);
		HIDDEN(_probepoints_start = .);
		*(.probepoint)
		HIDDEN(_probepoints_end = .);
		CONSTRUCTORS
		*(.got)
		*(.got.*)
		*(.sdata)
		*(.sdata.*)
		. = ALIGN(0x10);
	}
	.bss : {
		*(.sbss)
		*(.sbss.*)
		*(.scommon)
		*(.bss)
		*(.bss.*)
		*(COMMON)
		. = ALIGN(0x1000);
	}
	/DISCARD/ : { *(.interp) }
	/DISCARD/ : { *(.eh_frame) }
}
